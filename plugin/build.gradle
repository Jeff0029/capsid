plugins {
	// Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins
	// https://docs.gradle.org/current/userguide/java_gradle_plugin.html
	id 'java-gradle-plugin'

	// Provides the ability to publish build artifacts to an Apache Maven repository.
	// https://docs.gradle.org/current/userguide/publishing_maven.html
	id 'maven-publish'
}
apply plugin: "com.diffplug.spotless"
apply from: "https://gist.githubusercontent.com/yooksi/${spotlessGistID}/raw/spotless.gradle"

version '0.1.0'

repositories {
	mavenCentral()
}

dependencies {
	// https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.1'

	// https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.1'

	// https://mvnrepository.com/artifact/com.google.guava/guava
	implementation 'com.google.guava:guava:30.1-jre'

	// https://mvnrepository.com/artifact/org.jetbrains/annotations
	compileOnly 'org.jetbrains:annotations:20.1.0'
}

sourceSets {
	functionalTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
	integrationTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

gradlePlugin {
	//noinspection GroovyAssignabilityCheck
	plugins {
		capsid {
			id = 'io.pzstorm.capsid'
			implementationClass = 'io.pzstorm.capsid.CapsidPlugin'
		}
	}
	testSourceSets(sourceSets.integrationTest, sourceSets.functionalTest)
}

configurations {
	functionalTestImplementation.extendsFrom(implementation, testImplementation)
	integrationTestImplementation.extendsFrom(implementation, testImplementation)
}

// Register tasks to run functional and integration tests
tasks.register('functionalTest', Test) {
	it.useJUnitPlatform()
	it.testLogging.events "passed", "skipped", "failed"
	it.testClassesDirs = sourceSets.functionalTest.output.classesDirs
	it.classpath = sourceSets.functionalTest.runtimeClasspath
}
tasks.register('integrationTest', Test) {
	it.useJUnitPlatform()
	it.testLogging.events "passed", "skipped", "failed"
	it.testClassesDirs = sourceSets.integrationTest.output.classesDirs
	it.classpath = sourceSets.integrationTest.runtimeClasspath
}
test {
	useJUnitPlatform()
	testLogging.events "passed", "skipped", "failed"
}
tasks.named('check') {
	// Run functional and integration tests as part of `check`
	it.dependsOn(tasks.functionalTest, tasks.integrationTest)
}
